C********************************************************************
C
C  NOTA !!. Aquest programa es una modificacio del programa PERIFL
C           del "Study of Poincare Maps ...." i que esta al directori
C           habi a fi d'usar el model bicircular coherent en lloc
C           del bicircular que s'explicita en el Final Report pag 70.
C           S'han introduit varis canvis degut a que els moments
C           ara no son tan simples a partir de pos+vel com passa
C           amb el bicircular normal.
C
C
C  PROGRAM PERIFLC
C
C  THIS PROGRAM COMPUTES THE PERIODIC ORBIT OF THE BICIRCULAR PROBLEM
C  WHICH  SUBSTITUTES THE  EQUILIBRIUM POINT  OF THE RTBP. THEN IT
C  COMPUTES THE GENERATING FUNCTION G1 WHICH CANCELS THE LINEAR PART
C  OF THE EXPANSION OF THE (INITIAL REAL) HAMILTONIAN, AND THE FLOQUET
C  CHANGE OF VARIABLES WHICH PUTS THE SECOND ORDER PART IN CONSTANT
C  (AND SIMPLEST, IF DESIRED) FORM.
C  FOR THE COMPUTATIONS IT USES THE VECTORFIELD BICIRH WHICH CONTAINS
C  THE EQUATIONS AND VARIATIONAL EQUATIONS OF THE BICIRCULAR PROBLEM
C  IN HAMILTONIAN VARIABLES. (ALTHOUGH IT ASKS FOR MAGNITUDES IN
C  (X, XDOT, Y, YDOT, Z, ZDOT) THE COMPUTATIONS ARE PERFORMED IN
C  HAMILTONIAN VARIABLES (X, PX, Y, PY, Z, PZ).
C
C  INPUT VALUES:
C       - THE EQUILIBRIUM POINT LI WHICH IS SUBSTITUTED BY THE 
C         PERIODIC ORBIT.
C       - INITIAL APPROXIMATION OF THE PERIODIC SOLUTION (IT HAS
C         SEVERAL POSSIBILITIES IMPLEMENTED. ONE OF THEM IS TO USE
C         THE FILES ciolI.dat).
C       - DESIRED TYPE OF THE FINAL SECOND ORDER PART OF THE 
C         HAMILTONIAN. (TWO POSSIBILITIES ARE GIVEN: A CONSTANT
C         FORM OF THE COEFFICIENTS OR THE SIMPLEST ONE).
C       - NUMBER OF POINTS FOR PERFORMING THE FOURIER ANALYSIS
C         (IT GIVES A SUGGESTED VALUE).
C
C  OUTPUT VALUES IN THEIR RESPECTIVE FILES:
C       - THE GENERATING FUNCTION OF DEGREE ONE WHICH CANCELS THE
C         LINEAR PART OF THE HAMILTONIAN. (IT WILL BE NEEDED BY
C         PROGRAM EXPHA3 OR EXPHA4 OR EVEN BY FLOQUET).
C       - THE FLOQUET CHANGE OF VARIABLES WHICH PUTS THE SECOND
C         ORDER PART OF THE HAMILTONIAN IN THE DESIRED FORM.
C         (IT WILL BE NEEDED BY PROGRAM EXPHA3 OR EXPHA4).
C       - THE FLOQUET CHANGE OF VARIABLES RELATED WITH THE ADDED
C         MOMENTUM.(IT WILL BE NEEDED BY PROGRAM EXPHA3 OR EXPHA4).
C
C
C  NOTE: SEE PROGRAMS MATALIN AND FLOQUET FOR THE SAME COMPUTATIONS
C        IN A SEMIANALYTICAL WAY.
C
C  SUBROUTINES USED: PARAM, POSMAN, LIXY, RK78, SISTEMA,
C                    FLOR, ESCPOL, ESCVXY
C********************************************************************
        IMPLICIT REAL*8 (A-H,O-Z)
C
C  NEXT PARAMETERS MEAN THE FOLLOWING:
C     NOR = MAX DIMENSION ALLOWED FOR THE EXPANSION (SEE ROUTINE POSMAN).
C     NP1 = DIMENSION OF AN HOMOGENEOUS POLYNOMIAL OF DEGREE ONE.
C     NDQ = DIMENSION OF VECTOR CVXY, AT LEAST 20*LOF(1)
C     MAG2= DIMENSION OF VECTOR CVP, AT LEAST 13*LOF(1)
C     PRO=STOPPING CRITERIA FOR THE COMPUTATION OF THE PERIODIC ORBIT
        PARAMETER (NOR=12,MAG2=403,NDQ=20*31,KMM=20)
        PARAMETER (NP1=124,PRO=1.D-11)
        CHARACTER*75 ARX1,ARX
        COMPLEX*16 CVP(MAG2),CVXY(NDQ),G1(NP1)
        DIMENSION X(42),Y(6),FP(6),XMO(6,6),DIF(6,6),R(13,42),AL(8)
        DIMENSION B(42),F(42),IP(6),YA(6),CALFS(8,0:KMM)
        DIMENSION XMO1(6,6),XMO2(6,6),X1(6),X2(6)
        COMMON /CTANTS/LI,XMU,XMS,AS
        COMMON /BCOH/ NC1,XMU2,XMS2,WS,ALF,BET   !common de BICOHE
        COMMON /IEX/LH(NOR),LOF(NOR)
        EQUIVALENCE (X(7),XMO(1,1))
        EXTERNAL BICOHE
        CALL PARAM(WS)
        CALL POSMAN(NOA)
        IF (NOA.NE.NOR) THEN
        WRITE (NCS,*) 'PARAMETER NOR IS DIFFERENT FROM THE ONE'
        WRITE (NCS,*) 'IN ROUTINE POSMAN. NOR IN MAIN: ',NOR
        WRITE (NCS,*) 'NOR IN ROUTINE POSMAN: ',NOA
        STOP
        ENDIF
        XMU2=XMU
        XMS2=XMS
        AS2=AS
        PERI=8.D0*DATAN(1.D0)/WS
        NC1=7
        NCI=5
        NCS=6
        IF (13*LOF(1).GT.MAG2) THEN
        WRITE (NCS,*) 'MAIN. PARAM. MAG2 MUST BE AT LEAST: ',13*LOF(1)
        STOP
        ENDIF
        IF (20*LOF(1).GT.NDQ) THEN
        WRITE (NCS,*) 'MAIN. PARAMETER NDQ MUST BE AT LEAST: ',20*LOF(1)
        STOP
        ENDIF
C
C READING THE INPUT VALUES AND PERFORMING THE COMPUTATIONS.
C
1       WRITE (NCS,*) 'GIVE THE EQUILIBRIUM POINT (1-5) THAT IS '
        WRITE (NCS,*) 'SUBSTITUTED BY THE PERIODIC ORBIT WHICH'
        WRITE (NCS,*) 'MUST BE COMPUTED: '
        LI=2
C        READ (NCI,*) LI
        IF (LI.LT.1.OR.LI.GT.5) GOTO 1
        CALL LIXY(LI,XMU,ALF,BET)
        DO 3 I=1,6
        Y(I)=0.D0
3       CONTINUE
        Y(1)=ALF
        Y(3)=BET
        WRITE (NCS,*) 'X-Y SINODICAL COORD. OF THE RTBP-EQUIL. POINT:'
        WRITE (NCS,*) ALF,BET
        WRITE (NCS,*) 'I NEED THE INITIAL APROX. X,XDOT,Y,YDOT,Z,ZDOT'
        WRITE (NCS,*) 'IN SINODICAL COORD. FOR THE COMPUTATION OF THE'
        WRITE (NCS,*) 'PERIODIC ORBIT.'
        WRITE (NCS,*) 'DO YOU GIVE THEM BY HAND (1), IN A FILE (2) OR '
        WRITE (NCS,*) 'I TAKE THE EQUIL. POINT WITH ZERO VELOCITY (3) ?'
        ncas=2
C        READ (NCI,*) NCAS
        IF (NCAS.EQ.3) GOTO 2
        IF (NCAS.EQ.1) THEN
        WRITE (NCS,*) 'GIVE:  X,XDOT,Y,YDOT,Z,ZDOT'
        READ (NCI,*) Y(1),Y(2),Y(3),Y(4),Y(5),Y(6)
        ELSE
        WRITE (NCS,*) 'GIVE THE NAME OF THE FILE WHERE THEY ARE STORED'
        arx1='ciol2.dat'
C        READ (NCI,*) ARX1
        OPEN (NC1,FILE=ARX1,STATUS='OLD')
        READ (NC1,*) Y(1),Y(2),Y(3),Y(4),Y(5),Y(6)
        CLOSE(NC1)
        ENDIF
2       NTP=250
        WRITE (NCS,*) 'DO YOU WANT THE SYMPLECTIC CHANGE WHICH GIVES'
        WRITE (NCS,*) 'THE SECOND ORDER PART OF THE HAMILTONIAN IN'
        WRITE (NCS,*) 'ITS SIMPLEST CONSTANT FORM (THE FLOQUET MATRIX'
        WRITE (NCS,*) 'IN JORDAN FORM) (GIVE 1)'
        WRITE (NCS,*) 'OR DOU YOU WANT THE SECOND ORDER PART OF THE'
        WRITE (NCS,*) 'HAMILTONIAN ONLY IN CONSTANT FORM (GIVE 0)'
        ibj=1
C        READ (NCI,*) IBJ
        WRITE (NCS,*) 'GIVE THE NUMBER OF POINTS FOR PERFORMING'
        WRITE (NCS,*) 'THE FOURIER ANALYSIS OF THE PERIODIC'
        WRITE (NCS,*) 'MAGNITUDES, USUALLY: ',NTP
        ntp=250
C        READ (NCI,*) NTP
C THE COORDINATES ARE CHANGED INTO POSITIONS AND MOMENTA CENTERED AT THE
C EQUILIBRIUM POINT. THE COORDINATES OF THE VECTORFIELD BICIRH.
C En aquest cas el canvi involucra els coeficients periodics del Ham
        CALL LLCBC(XMU,XMS,WS,CALFS,KM,KMM,NC1)
        CALL VALAL(0.D0,CALFS,KM,AL)
        Y(1)=Y(1)-ALF
        Y(3)=Y(3)-BET
        Y(2)=(Y(2)-AL(2)*Y(1)-AL(3)*Y(3)-ALF*AL(2)+
     &        BET*(AL(1)-AL(3)))/AL(1)
        Y(4)=(Y(4)-AL(2)*Y(3)+AL(3)*Y(1)+ALF*(AL(3)-AL(1))-
     &        BET*AL(2))/AL(1)
        write (*,*) 'x,px,y,py,z,pz inicials:'
        write (*,*) (y(i),i=1,6)
        WRITE (NCS,*) 'COMPUTATION OF THE PERIODIC ORBIT.'
        ITER=0
10      ITER=ITER+1
        WRITE (NCS,*) 'ITERATION NUMBER: ',ITER
        CALL ENDENR(Y,PERI,X1,X2,XMO1,XMO2,BICOHE)
        ERF=0.D0
        DO 20 I=1,6
        FP(I)=X1(I)-X2(I)
        ERF=ERF+FP(I)*FP(I)
        DO 21 J=1,6
        DIF(J,I)=XMO1(J,I)-XMO2(J,I)
21      CONTINUE
20      CONTINUE
        ERF=DSQRT(ERF)
        WRITE (NCS,*) 'NORM OF THE FUNCTION: ',ERF
        CALL SISTEMA(DIF,FP,FP,6,1.D-10,IP,F)
        ERC=0.D0
        DO 25 I=1,6
        Y(I)=Y(I)-FP(I)
        ERC=ERC+FP(I)*FP(I)
25      CONTINUE
        ERC=DSQRT(ERC)
        WRITE (NCS,*) 'NORM OF THE CORRECTION: ',ERC
        IF (ERF.GT.PRO) GOTO 10
        WRITE (NCS,*) 'PERIODIC ORBIT FOUND ! '
        WRITE (NCS,*) 'SYNODICAL COORD. X,Y,Z,XD,YD,ZD:'
C En aquest canvi cal tornar a usar les alfes
        YA(1)=Y(1)+ALF
        YA(2)=Y(3)+BET
        YA(3)=Y(5)
        YA(4)=AL(1)*Y(2)+AL(2)*Y(1)+AL(3)*Y(3)+ALF*AL(2)-
     &        BET*(AL(1)-AL(3))
        YA(5)=AL(1)*Y(4)+AL(2)*Y(3)-AL(3)*Y(1)+ALF*(AL(1)-AL(3))+
     &        BET*AL(2)
        YA(6)=Y(6)
        WRITE  (NCS,*) (YA(I),I=1,6)
C
C COMPUTING THE FOURIER ANALYSIS OF THE GENERATING FUNCTION G1
C AND THE FLOQUET CHANGE OF VARIABLES.
C
        CALL FLOR(Y,WS,NTP,G1,CVXY,CVP,IBJ,LOF(1),LOF(2))
C
C WRITTING THE OBTAINED RESULTS.
C
        WRITE (NCS,*) 'GIVE THE NAME OF THE OUTPUT FILE WHICH'
        WRITE (NCS,*) 'WILL CONTAIN THE GENERATING FUNCTION G1'
        READ (NCI,*) ARX
        CALL ESCPOL(G1,1,LOF(1),ARX,NCE)
        WRITE (NCS,*) 'GIVE THE NAME OF THE OUTPUT FILE WHICH'
        WRITE (NCS,*) 'WILL CONTAIN THE CHANGE OF VARIABLES WITH'
        WRITE (NCS,*) 'RESPECT TO X1,Y1,X2,Y2,X3,Y3: '
        READ (NCI,*) ARX
        CALL ESCVXY(CVXY,LOF(1),ARX,NCE)
        WRITE (NCS,*) 'GIVE THE NAME OF THE OUTPUT FILE WHICH'
        WRITE (NCS,*) 'WILL CONTAIN THE PART OF THE CHANGE OF'
        WRITE (NCS,*) 'VARIABLES DUE TO THE ADDED MOMENTUM'
        READ (NCI,*) ARX
        CALL ESCPOL(CVP,2,LOF(2),ARX,NCE)
        WRITE (NCS,*) 'PROGRAM PERIFL SUCCESSFULLY COMPLETED'
        END
